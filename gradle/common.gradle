// Shared Gradle configuration for all subprojects

apply from: rootProject.file("gradle/dependencies.gradle")

group = "ru.endlesscode.${rootProject.name}"
description = rootProject.description
archivesBaseName = "$pluginName-$name"

// Java version
sourceCompatibility = targetCompatibility = "1.8"

// Enable coroutines
kotlin {
    experimental {
        coroutines 'enable'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = "sources"
}

jar.dependsOn sourcesJar

afterEvaluate {
    // Set it again in case ForgeGradle or BukkitGradle resets it
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url = "https://jitpack.io" }
}

dependencies {
    applyDeps(commonDeps) { compileOnly(it) }
}

ext.pomConfig = {
    url "https://gitlab.com/endlesscodegroup/inspector"

    licenses {
        license {
            name "GNU Affero General Public License v3.0"
            url "https://www.gnu.org/licenses/agpl-3.0.html"
        }
    }

    developers {
        developer {
            id "endlesscodegroup"
            name "Endless Code Group"
        }
        developer {
            id "osipxd"
            name "Osip Fatkullin"
            email "osip.fatkullin@gmail.com"
        }
    }
}
