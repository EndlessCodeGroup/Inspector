def repoUrl = "https://gitlab.com/endlesscodegroup/inspector"

bukkit {
    version = "1.13"

    meta {
        name = pluginName
        main = "$group.${project.name}.$pluginName"
        authors = ["OsipXD", "EndlessCodeGroup"]
        url = repoUrl
    }

    run {
        core = "spigot"
        eula = true
    }
}

dependencies {
    compileOnly(bukkit())
    implementation(bukkitDeps.reflections) {
        // Guava already in Bukkit
        exclude(module: "guava")
    }
}

shadowJar {
    relocate("kotlin", "ru.endlesscode.inspector.shade.kotlin")
    relocate("khttp", "ru.endlesscode.inspector.shade.khttp")
    relocate("javassist", "ru.endlesscode.inspector.shade.javassist")
    relocate("org.json", "ru.endlesscode.inspector.shade.org.json")
    relocate("org.intellij", "ru.endlesscode.inspector.shade.org.intellij")
    relocate("org.jetbrains", "ru.endlesscode.inspector.shade.org.jetbrains")
    relocate("org.reflections", "ru.endlesscode.inspector.shade.org.reflections")
}

assemble.dependsOn shadowJar

apply(from: rootProject.file("gradle/publish.gradle"))

publishing {
    publications {
        bukkit(MavenPublication) {
            artifactId "$rootProject.name-$name"

            from components.java
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode("name", "Inspector (Bukkit)")
                root.appendNode("description", "Bukkit implementation of Inspector API")
                root.children().last() + pomConfig

                // Remove not needed dependencies
                def artifactsToFilter = ["kotlin-stdlib-jdk8", "kotlinx-coroutines-core", "reflections"]
                root.dependencies.'*'.findAll() {
                    it.artifactId.text() in artifactsToFilter
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}
