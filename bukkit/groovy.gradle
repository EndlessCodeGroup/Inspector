def repoUrl = "https://gitlab.com/endlesscodegroup/inspector"

bukkit {
    version = "1.8"

    meta {
        name = pluginName
        main = "$group.${project.name}.$pluginName"
        authors = ["OsipXD", "EndlessCodeGroup"]
        url = repoUrl
    }

    run {
        core = "paper"
        eula = true
    }
}

dependencies {
    compileOnly(bukkit())
    applyDeps(commonDeps) { implementation(it) }
    applyDeps(bukkitDeps) { compileOnly(it) }
}

shadowJar {
    relocate("kotlin", "ru.endlesscode.inspector.shade.kotlin")
    relocate("khttp", "ru.endlesscode.inspector.shade.khttp")
    relocate("org.json", "ru.endlesscode.inspector.shade.org.json")

    dependencies {
        // Exclude annotations from final jar
        exclude(dependency("org.jetbrains:annotations:13.0"))
    }
}

assemble.dependsOn shadowJar

def pomConfig = {
    licenses {
        license {
            name "GNU Affero General Public License v3.0"
            url "https://www.gnu.org/licenses/agpl-3.0.html"
        }
    }

    developers {
        developer {
            id "endlesscodegroup"
            name "Endless Code Group"
        }
        developer {
            id "osipxd"
            name "Osip Fatkullin"
            email "osip.fatkullin@gmail.com"
        }
    }
}

publishing {
    publications {
        bukkit(MavenPublication) {
            artifactId "$rootProject.name-$name"

            artifact shadowJar

            pom.withXml {
                def root = asNode()
                root.appendNode("description", project.description)
                root.children().last() + pomConfig
            }
        }
        api(MavenPublication) {
            artifactId "$rootProject.name-$apiProject.name"

            artifact apiProject.jar
            pom.withXml {
                def root = asNode()
                root.appendNode("description", apiProject.description)
                root.children().last() + pomConfig
            }
        }
    }
}
