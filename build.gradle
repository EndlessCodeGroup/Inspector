import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Root project build config
plugins {
    id("base")
    id("com.github.ben-manes.versions") version "0.27.0"
    id("org.jetbrains.kotlin.jvm") version "1.4.21" apply false
    id("bintray-credentials")
}

description = "Catch and report all exceptions"

allprojects {
    ext.apiProject = project(":inspector-api")
}

// Common configurations for all subprojects
subprojects {
    apply(plugin: "kotlin")
    apply(from: rootProject.file("gradle/common.gradle"))
    
    version = "0.9"

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.3"
            languageVersion = "1.3"
        }
    }
}

// Configuration for API implementations
configure(subprojects.findAll { it.name.startsWith(project.name) } - apiProject) {

    // Finish configuring API first
    evaluationDependsOn(apiProject.path)

    // Use api as dependency
    dependencies {
        api(apiProject)
    }
}
