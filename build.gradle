// Root project build config

buildscript {
    ext.kotlinVersion = '1.2.60'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"

        constraints {
            classpath("org.apache.httpcomponents:httpclient:4.5.2") {
                because("it's needed for gradle-download-task plugin")
            }
        }
    }
}

plugins {
    id "base"
}

description = "Catch and report all exceptions"

allprojects {
    ext.pluginName = "Inspector"
    ext.apiProject = project(":api")
    ext.inspectorVersion = "0.3"
}

// Common configurations for all subprojects
subprojects {
    apply(plugin: "kotlin")
    apply(from: rootProject.file("gradle/common.gradle"))

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.2"
            languageVersion = "1.2"
            freeCompilerArgs = ["-Xenable-jvm-default"]
        }
    }

    apply(from: rootProject.file("gradle/publish.gradle"))
}

// Configuration for API implementations
configure(subprojects - apiProject) {

    // Finish configuring API first
    evaluationDependsOn(apiProject.path)

    // Use api as dependency
    dependencies {
        compile(apiProject)
    }
}
