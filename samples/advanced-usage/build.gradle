import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id("ru.endlesscode.bukkitgradle") version "0.10.0"
    id("com.github.johnrengelman.shadow") version "7.0.0"
}

group = "com.example"
description = "My Bukkit plugin with Discord Reporter"
version = "0.1-SNAPSHOT"

// Read more about BukkitGradle: https://github.com/EndlessCodeGroup/BukkitGradle
bukkit {
    apiVersion = "1.17.1"

    meta {
        name.set("MyPlugin")
        url.set("https://www.example.com")
        authors.set(["osipxd"])
    }

    server {
        core = "spigot"
        eula = true
    }
}

shadowJar {
    minimize()

    // Exclude dependencies bundled into Spigot from resulting JAR
    dependencies {
        exclude(dependency("com.google.code.gson:gson:.*"))
        exclude(dependency("org.jetbrains:annotations:.*"))
    }
}

// Here we use automatic relocation
task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "shadow.com.example.myplugin"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar
tasks.assemble.dependsOn tasks.shadowJar

ext.inspectorVerson = "0.12.0"

dependencies {
    compileOnly(spigotApi()) {
        transitive = false
    }
    implementation "ru.endlesscode.inspector:inspector-bukkit:$inspectorVerson"
    implementation "ru.endlesscode.inspector:inspector-discord-reporter:$inspectorVerson"
}

repositories {
    mavenCentral()
}
