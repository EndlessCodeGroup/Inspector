plugins {
    id 'ru.endlesscode.bukkitgradle' version '0.10.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = "com.example.myplugin"
description = "My Bukkit plugin with Sentry Reporter"
version = "0.1-SNAPSHOT"

// Read more about BukkitGradle: https://github.com/EndlessCodeGroup/BukkitGradle
bukkit {
    apiVersion = "1.17.1"

    meta {
        name.set("MyPlugin")
        url.set("http://www.example.com")
        authors.set(["osipxd"])
    }

    server {
        core = "spigot"
        eula = true
    }
}

shadowJar {
    minimize()

    def shadowPackage = "shadow.com.example.myplugin"
    relocate "ru.endlesscode.inspector", "${shadowPackage}.inspector"
    relocate "org.jetbrains", "${shadowPackage}.jetbrains"
    relocate "kotlinx", "${shadowPackage}.kotlinx"
    relocate "kotlin", "${shadowPackage}.kotlin"
    relocate "io.sentry", "${shadowPackage}.sentry"
    relocate "org.slf4j", "${shadowPackage}.slf4j"
    relocate "com.fasterxml.jackson.core", "${shadowPackage}.jackson"
}
tasks.assemble.dependsOn tasks.shadowJar

def inspectorVersion = "0.11.0"

dependencies {
    compileOnly(spigotApi()) { transitive = false }
    implementation("ru.endlesscode.inspector:inspector-bukkit:$inspectorVersion")
    implementation("ru.endlesscode.inspector:inspector-sentry-reporter:$inspectorVersion")
    implementation("ru.endlesscode.inspector:sentry-bukkit:$inspectorVersion")
}

repositories {
    mavenCentral()
}
